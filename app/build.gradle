apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"

android {
	// todo add signingConfigs
	compileSdkVersion 27
	buildToolsVersion "27.0.2"
	defaultConfig {
		applicationId "ru.fantlab.android"
		// todo backport to 16
		minSdkVersion 21
		targetSdkVersion 27
		versionCode 13
		versionName "0.1.3"
		archivesBaseName = "fantlab-${versionName}"
		buildConfigField "String", "REST_URL", '"https://api.fantlab.ru/"'
		javaCompileOptions {
			annotationProcessorOptions {
				includeCompileClasspath false
			}
		}
	}
	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			signingConfig signingConfigs.debug
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			ext.alwaysUpdateBuildId = false
		}
	}
	sourceSets {
		main {
			java.srcDirs = [
				"src/main/kotlin"
			]
			res.srcDirs = [
				"src/main/res/",
				"src/main/res/layouts/main_layouts",
				"src/main/res/layouts/row_layouts",
				"src/main/res/layouts/other_layouts"
			]
		}
	}
	lintOptions {
		warningsAsErrors true
		abortOnError true
		htmlReport true
		disable "InvalidPackage"
	}
	configurations {
		all*.exclude module: "annotations"
	}
	packagingOptions {
		exclude "META-INF/NOTICE"
		exclude "META-INF/NOTICE.txt"
		exclude "META-INF/LICENSE"
		exclude "META-INF/LICENSE.txt"
		exclude "META-INF/rxjava.properties"
	}
	dexOptions {
		jumboMode true
		javaMaxHeapSize "4g"
	}
}

kapt {
	generateStubs = true
}

repositories {
	maven { url "https://jitpack.io" }
	maven { url "https://maven.fabric.io/public" }
}

dependencies {
	implementation "com.android.support:appcompat-v7:${support_version}"
	implementation "com.android.support:design:${support_version}"
	implementation "com.android.support:cardview-v7:${support_version}"
	implementation "com.android.support:recyclerview-v7:${support_version}"
	implementation "com.android.support:preference-v14:${support_version}"
	implementation "net.grandcentrix.thirtyinch:thirtyinch:${thirtyinch_version}"
	implementation "net.grandcentrix.thirtyinch:thirtyinch-rx2:${thirtyinch_version}"
	implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
	implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"
	implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofit_version}"
	implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"
	implementation "com.github.bumptech.glide:glide:${glide_version}"
	implementation "com.jakewharton:butterknife:${butterKnife_version}"
	implementation "io.reactivex.rxjava2:rxjava:2.1.6"
	implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
	implementation "com.github.GrenderG:Toasty:1.1.3"
	implementation "io.requery:requery:${requery_version}"
	implementation "io.requery:requery-android:${requery_version}"
	implementation("com.crashlytics.sdk.android:crashlytics:2.7.1@aar") { transitive = true }
	implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
	implementation "com.evernote:android-state:${state_version}"
	implementation "com.facebook.stetho:stetho:${stetho_version}"
	implementation "com.facebook.stetho:stetho-okhttp3:${stetho_version}"
	implementation "com.jakewharton.timber:timber:4.5.1"
	implementation 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:2.0.2'
	implementation 'cn.gavinliu.android.lib:ShapedImageView:0.8.3'
	implementation 'com.github.matthiasrobbers:shortbread:1.0.1'
	implementation 'com.journeyapps:zxing-android-embedded:3.2.0@aar'
	implementation 'com.google.zxing:core:3.2.1'
	kapt "com.evernote:android-state-processor:${state_version}"
	kapt "com.jakewharton:butterknife-compiler:${butterKnife_version}"
	kapt "io.requery:requery-processor:${requery_version}"
	kapt 'com.github.matthiasrobbers:shortbread-compiler:1.0.1'
	testImplementation "junit:junit:${junit_version}"
}