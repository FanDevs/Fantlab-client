apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"

ext {
	min_sdk = 21
	compile_sdk = 27
	target_sdk = 27
	build_tools = '27.0.3'
	version_code = 14
	version_name = '0.1.4'

	annotations = '16.0.2'
	support = '27.1.1'
	thirtyinch = '0.8.0'
	rxjava = '2.1.12'
	rxandroid = '2.0.2'
	fuel = '1.12.1'
	gson = '2.8.2'
	room = '1.1.1'
	android_state = '1.2.1'
	butterknife = '8.5.1'
	zxing = '3.3.0'
	zxing_embedded = '3.6.0'
	glide = '3.8.0'
	shaped = '0.8.3'
	toasty = '1.1.3'
	bottom_navigation = '2.0.2'
	shortbread = '1.0.1'
	timber = '4.5.1'
	stetho = '1.5.0'
	commonmark = '0.10.0'
	htmlSpanner = '0.4'
	yslibrary = '2.1.0'
	// назвать переменную crashlytics нельзя
	crashlytics_version = '2.9.1'
}

android {
	compileSdkVersion compile_sdk
	buildToolsVersion build_tools
	defaultConfig {
		applicationId "ru.fantlab.android"
		minSdkVersion min_sdk
		targetSdkVersion target_sdk
		versionCode version_code
		versionName version_name
		archivesBaseName = "fantlab-${versionName}"
		buildConfigField "String", "REST_URL", '"https://api.fantlab.ru/"'
		buildConfigField "String", "API_VERSION", '"0.9.17"'
		javaCompileOptions {
			annotationProcessorOptions {
				includeCompileClasspath false
			}
		}
		kapt {
			arguments {
				arg("room.schemaLocation", "$projectDir/schemas".toString())
			}
		}
	}
	buildTypes {
		release {
			minifyEnabled true
			shrinkResources true
			signingConfig signingConfigs.debug
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			ext.alwaysUpdateBuildId = false
		}
	}
	sourceSets {
		main {
			java.srcDirs = [
				"src/main/kotlin"
			]
			res.srcDirs = [
				"src/main/res/",
				"src/main/res/layouts/main_layouts",
				"src/main/res/layouts/row_layouts",
				"src/main/res/layouts/other_layouts"
			]
		}
	}
	lintOptions {
		warningsAsErrors true
		abortOnError true
		htmlReport true
		disable "InvalidPackage"
	}
	configurations {
		all*.exclude module: "annotations"
	}
	packagingOptions {
		exclude "META-INF/NOTICE"
		exclude "META-INF/NOTICE.txt"
		exclude "META-INF/LICENSE"
		exclude "META-INF/LICENSE.txt"
		exclude "META-INF/rxjava.properties"
	}
	dexOptions {
		jumboMode true
		javaMaxHeapSize "4g"
	}
	androidExtensions {
		experimental = true
	}
}

kapt {
	generateStubs = true
}

repositories {
	maven { url "https://jitpack.io" }
	maven { url "https://maven.fabric.io/public" }
}

dependencies {
	implementation "com.android.support:appcompat-v7:$support"
	implementation "com.android.support:design:$support"
	implementation "com.android.support:cardview-v7:$support"
	implementation "com.android.support:recyclerview-v7:$support"
	implementation "com.android.support:preference-v14:$support"

	implementation "org.jetbrains:annotations-java5:$annotations"
	implementation "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinch"
	implementation "net.grandcentrix.thirtyinch:thirtyinch-rx2:$thirtyinch"
	implementation "io.reactivex.rxjava2:rxjava:$rxjava"
	implementation "io.reactivex.rxjava2:rxandroid:$rxandroid"
	implementation "com.github.kittinunf.fuel:fuel-android:$fuel"
	implementation "com.github.kittinunf.fuel:fuel-rxjava:$fuel"
	implementation "com.github.kittinunf.fuel:fuel-gson:$fuel"
	implementation "android.arch.persistence.room:runtime:$room"
	implementation "android.arch.persistence.room:rxjava2:$room"
	kapt "android.arch.persistence.room:compiler:$room"
	implementation "com.evernote:android-state:$android_state"
	kapt "com.evernote:android-state-processor:$android_state"
	implementation "com.jakewharton:butterknife:$butterknife"
	kapt "com.jakewharton:butterknife-compiler:$butterknife"
	implementation "com.github.bumptech.glide:glide:$glide"
	implementation "com.google.zxing:core:$zxing"
	implementation("com.journeyapps:zxing-android-embedded:$zxing_embedded@aar") { transitive = false }
	implementation "com.github.matthiasrobbers:shortbread:$shortbread"
	kapt "com.github.matthiasrobbers:shortbread-compiler:$shortbread"
	implementation "it.sephiroth.android.library.bottomnavigation:bottom-navigation:$bottom_navigation"
	implementation "com.github.GrenderG:Toasty:$toasty"
	implementation "cn.gavinliu.android.lib:ShapedImageView:$shaped"
	implementation "com.jakewharton.timber:timber:$timber"
	implementation("com.crashlytics.sdk.android:crashlytics:$crashlytics_version@aar") { transitive = true }
	implementation "com.facebook.stetho:stetho:$stetho"
	implementation "com.facebook.stetho:stetho-okhttp3:$stetho"
	implementation "com.atlassian.commonmark:commonmark:${commonmark}"
	implementation "com.atlassian.commonmark:commonmark-ext-autolink:${commonmark}"
	implementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:${yslibrary}"
	implementation "com.github.nightwhistler:HtmlSpanner:${htmlSpanner}"
}