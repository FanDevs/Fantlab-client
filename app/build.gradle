apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"
apply plugin: "com.github.ben-manes.versions"
apply plugin: 'com.novoda.build-properties'

ext {
	min_sdk = 21
	compile_sdk = 28
	target_sdk = 28
	version_code = 24
	version_name = '0.2.1'

	annotations = '17.0.0'
	compat = '1.1.0-rc01'
	material = '1.1.0-alpha09'
	preference = '1.0.0'
	thirtyinch = '0.9.0'
	rxjava = '2.2.7'
	rxandroid = '2.1.1'
	ktx = '1.1.0-alpha05'
	koptional = '1.6.0'
	fuel = '2.0.1'
	gson = '2.8.5'
	room = '1.1.1'
	android_state = '1.4.1'
	zxing = '3.4.0'
	zxing_embedded = '3.6.0'
	glide = '4.9.0'
	shaped = '0.8.6'
	toasty = '1.4.0'
	bottom_navigation = '2.0.2'
	shortbread = '1.0.2'
	timber = '4.7.1'
	stetho = '1.5.0'
	colorpicker = '1.1.4'
	svg = '1.4'
	room = '2.1.0'
	// назвать переменную crashlytics нельзя
	crashlytics_version = '2.9.9'
}

buildProperties {
	secrets {
		using(rootProject.file("gradle.properties")).or(rootProject.file("gradle_debug.properties"))
	}
}

android {
	compileSdkVersion compile_sdk
	signingConfigs {
		signing {
			storeFile file(buildProperties.secrets['RELEASE_STORE_FILE'].string)
			keyAlias buildProperties.secrets['RELEASE_KEY_ALIAS'].string
			keyPassword buildProperties.secrets['RELEASE_KEY_PASSWORD'].string
			storePassword buildProperties.secrets['RELEASE_STORE_PASSWORD'].string
		}
	}
	defaultConfig {
		applicationId "ru.fantlab.android"
		minSdkVersion min_sdk
		targetSdkVersion target_sdk
		versionCode version_code
		versionName version_name
		archivesBaseName = "fantlab-${versionName}(${versionCode})"
		buildConfigField "String", "REST_URL", '"https://api.fantlab.ru/"'
		buildConfigField "String", "API_VERSION", '"0.9.3"'
		resValue "string", "VERSION_NAME", "$versionName ($versionCode)"
		javaCompileOptions {
			annotationProcessorOptions {
				includeCompileClasspath false
			}
		}
		kapt {
			arguments {
				arg("room.schemaLocation", "$projectDir/schemas".toString())
			}
		}
	}
	buildTypes {
		debug {
			signingConfig signingConfigs.signing
			ext.alwaysUpdateBuildId = false
		}
		release {
			minifyEnabled true
			shrinkResources true
			signingConfig signingConfigs.signing
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}
	sourceSets {
		main {
			java.srcDirs = ["src/main/kotlin"]
			res.srcDirs = ["src/main/res/",
						   "src/main/res/layouts/main_layouts",
						   "src/main/res/layouts/row_layouts",
						   "src/main/res/layouts/other_layouts"]
		}
	}
	lintOptions {
		warningsAsErrors true
		abortOnError true
		htmlReport true
		disable "InvalidPackage"
	}
	packagingOptions {
		exclude "META-INF/NOTICE"
		exclude "META-INF/NOTICE.txt"
		exclude "META-INF/LICENSE"
		exclude "META-INF/LICENSE.txt"
		exclude "META-INF/rxjava.properties"
	}
	dexOptions {
		jumboMode true
		javaMaxHeapSize "4g"
	}
	androidExtensions {
		experimental = true
	}
}

dependencyUpdates {
	revision = "release"
	outputFormatter = "json"
	outputDir = "build/dependencyUpdates"
}

repositories {
	maven { url "https://jitpack.io" }
	maven { url "https://maven.fabric.io/public" }
}

dependencies {
	implementation "androidx.appcompat:appcompat:$compat"
	implementation "com.google.android.material:material:$material"
	implementation "androidx.legacy:legacy-preference-v14:$preference"

	implementation "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinch"
	implementation "net.grandcentrix.thirtyinch:thirtyinch-rx2:$thirtyinch"
	implementation "io.reactivex.rxjava2:rxjava:$rxjava"
	implementation "io.reactivex.rxjava2:rxandroid:$rxandroid"
	implementation "androidx.core:core-ktx:$ktx"

	implementation "com.gojuno.koptional:koptional:$koptional"
	implementation "com.github.kittinunf.fuel:fuel-android:$fuel"
	implementation "com.github.kittinunf.fuel:fuel-rxjava:$fuel"
	implementation "com.github.kittinunf.fuel:fuel-gson:$fuel"
	implementation "androidx.room:room-runtime:$room"
	implementation "androidx.room:room-rxjava2:$room"
	kapt "androidx.room:room-compiler:$room"
	implementation "com.evernote:android-state:$android_state"
	kapt "com.evernote:android-state-processor:$android_state"

	implementation "com.github.bumptech.glide:glide:$glide"
	kapt "com.github.bumptech.glide:compiler:$glide"

	implementation "com.google.zxing:core:$zxing"
	implementation("com.journeyapps:zxing-android-embedded:$zxing_embedded@aar") {
		transitive = false
	}
	implementation "com.github.matthiasrobbers:shortbread:$shortbread"
	kapt "com.github.matthiasrobbers:shortbread-compiler:$shortbread"
	implementation "it.sephiroth.android.library.bottomnavigation:bottom-navigation:$bottom_navigation"
	implementation "com.github.GrenderG:Toasty:$toasty"
	implementation "cn.gavinliu:ShapedImageView:$shaped"
	implementation "com.jakewharton.timber:timber:$timber"
	implementation("com.crashlytics.sdk.android:crashlytics:$crashlytics_version@aar") {
		transitive = true
	}
	implementation "com.google.firebase:firebase-core:16.0.8"
	implementation "com.facebook.stetho:stetho:$stetho"
	implementation "com.facebook.stetho:stetho-okhttp3:$stetho"
	implementation "petrov.kristiyan:colorpicker-library:$colorpicker"
	implementation "com.caverock:androidsvg-aar:$svg"
}

apply plugin: 'com.google.gms.google-services'